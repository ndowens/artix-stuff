# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=alsa-plugins
pkgver=1.2.2
pkgrel=1
pkgdesc="Additional ALSA plugins"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
depends=(glibc)
makedepends=(alsa-lib dbus jack libavtp libpulse libsamplerate speexdsp)
optdepends=('dbus: for maemo plugin'
            'jack: for pcm_jack plugin'
            'libavtp: for pcm_aaf plugin'
            'libsamplerate: for rate_samplerate plugin'
            'libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
            'speexdsp: for pcm_speex and rate_speexrate plugins')
source=("https://www.alsa-project.org/files/pub/plugins/$pkgname-$pkgver.tar.bz2")
sha256sums=('1c0f06450c928d711719686c9dbece2d480184f36fab11b8f0534cb7b41e337d')

prepare() {
  cd $pkgname-$pkgver

  # a52, lavrate plugins not compatible with ffmpeg >= 4.0
  # https://mailman.alsa-project.org/pipermail/alsa-devel/2019-December/159425.html
  # https://bugs.archlinux.org/task/60586
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-maemo-plugin \
    --enable-maemo-resource-manager
  make
}

package() {
  depends+=(libasound.so)

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 doc/README* doc/*.txt
  # remove maemo plugin configuration (it overrides defaults for pcm and ctl),
  # until a better way is found to package default overrides:
  # https://bugs.archlinux.org/task/65440
  rm -v "${pkgdir}/etc/alsa/conf.d/98-maemo.conf"
}

# vim:set sw=2 et:
