# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Radu Andries <admiral0@tuxfamily.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgbase=zbar
pkgname=(zbar zbar-gtk zbar-qt python-zbar)
pkgver=0.23.1
pkgrel=1
pkgdesc="Application and library for reading bar codes from various sources"
arch=('x86_64')
url="https://github.com/mchehab/zbar"
license=('LGPL')
makedepends=('gtk3' 'imagemagick' 'libxv' 'v4l-utils' 'qt5-x11extras' 'python' 'xmlto'
             'docbook-xsl' 'gobject-introspection')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mchehab/zbar/archive/$pkgver.tar.gz")
sha512sums=('ae7741cf750a10cf53dc11abcd482c3885507153ee37f6e3364ed5ed72184ebb009560b8c40d8090603a551fb681700a962838a59ce77d005d080ee49fbfa54b')

build() {
  cd zbar-$pkgver
  autoreconf -vfi
  ./configure --prefix=/usr --with-qt --with-gtk=gtk3 CFLAGS="$CFLAGS -DNDEBUG"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_zbar() {
  depends=('dbus' 'imagemagick' 'libsm' 'libxv' 'v4l-utils')

  cd zbar-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/lib/libzbar{gtk,qt}.so* \
        "$pkgdir"/usr/lib/*.a \
        "$pkgdir"/usr/include/zbar/zbargtk.h \
        "$pkgdir"/usr/include/zbar/QZBar*.h \
        "$pkgdir"/usr/lib/pkgconfig/zbar-{gtk,qt}.pc \
        "$pkgdir"/usr/lib/python3.8 \
        "$pkgdir"/usr/bin/zbarcam-* \
        "$pkgdir"/usr/share/gir-1.0 \
        "$pkgdir"/usr/lib/girepository-1.0

  rm -rf "$pkgdir"/etc{/dbus-1/system.d,/dbus-1,}
}

package_zbar-gtk() {
  depends=('zbar' 'gtk3')

  cd zbar-$pkgver
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/include/zbar/zbargtk.h ./

  rm -r "$pkgdir"/usr/lib/libzbar{,qt}.so* \
        "$pkgdir"/usr/lib/*.a \
        "$pkgdir"/usr/include/zbar/*.h \
        "$pkgdir"/usr/include/zbar.h \
        "$pkgdir"/usr/lib/pkgconfig/zbar{,-qt}.pc \
        "$pkgdir"/usr/lib/python3.8 \
        "$pkgdir"/usr/share/{doc,man} \
        "$pkgdir"/usr/bin/{zbarimg,zbarcam,zbarcam-qt} \
        "$pkgdir"/etc

  mv ./zbargtk.h "$pkgdir"/usr/include/zbar/
}

package_zbar-qt() {
  depends=('zbar' 'qt5-x11extras')

  cd zbar-$pkgver
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/include/zbar/QZBar*.h ./

  rm -r "$pkgdir"/usr/lib/libzbar{,gtk}.so* \
        "$pkgdir"/usr/lib/*.a \
        "$pkgdir"/usr/include/zbar/*.h \
        "$pkgdir"/usr/include/zbar.h \
        "$pkgdir"/usr/lib/pkgconfig/zbar{,-gtk}.pc \
        "$pkgdir"/usr/lib/python3.8 \
        "$pkgdir"/usr/share \
        "$pkgdir"/usr/bin/{zbarimg,zbarcam,zbarcam-gtk} \
        "$pkgdir"/etc \
        "$pkgdir"/usr/lib/girepository-1.0

  mv ./QZBar*.h "$pkgdir"/usr/include/zbar/
}

package_python-zbar() {
  depends=('python' 'zbar')

  cd zbar-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/lib/*.so* \
        "$pkgdir"/usr/lib/*.a \
        "$pkgdir"/usr/include \
        "$pkgdir"/usr/lib/pkgconfig \
        "$pkgdir"/usr/{bin,share} \
        "$pkgdir"/etc \
        "$pkgdir"/usr/lib/girepository-1.0
}

# vim:set ts=2 sw=2 et:
