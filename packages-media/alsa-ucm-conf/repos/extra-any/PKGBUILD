# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.2
pkgrel=1
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/$pkgname-$pkgver.tar.bz2")
sha512sums=('25e3f341c110772c7cffc8c7745b320f2ddd12fe6460380c088939b5dc67b99ae4ad92e69375179574e0e1c40d14ff49961e86494e223fde009e51590f00ca8f')
b2sums=('ce850415a115f673267f6ab32f176c2ab73aad8bd7cf2be244bfbb7342756c6de2212044cb92b3524dc4189955c8144b134af144b6a15aabf33c06cbcfe1fdf6')

package() {
  cd "${pkgname}-${pkgver}"
  for dir in ucm2/*; do
    if [ -d "$dir" ] && [[ "$dir" == */codecs ]] || [[ "$dir" == */dsps ]] || [[ "$dir" == */platforms ]]; then
      for subdir in "${dir}/"*; do
        install -vDm 644 "${subdir}"/*.conf -t "$pkgdir/usr/share/alsa/ucm2/$(basename $dir)/$(basename $subdir)"
      done
    else
      [ -d "$dir" ] && install -vDm 644 "${dir}"/*.conf -t "$pkgdir/usr/share/alsa/ucm2/$(basename $dir)"
    fi
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
