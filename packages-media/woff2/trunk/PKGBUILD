# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=woff2
pkgver=1.0.2
pkgrel=2
pkgdesc="Web Open Font Format 2 reference implementation"
url="https://github.com/google/woff2"
arch=(x86_64)
license=(custom:MIT)
depends=(brotli)
makedepends=(git cmake ninja)
_commit=1bccf208bca986e53a647dfe4811322adb06ecf8  # tags/v1.0.2
source=("git+${url}#commit=$_commit"
	"checkoverflow.patch::${url}/commit/3831354113db8803fb1f5ba196cf0bbb537578dd.patch")
sha512sums=('SKIP'
            '78fcb6a35b3339c5e43816a67b5b48f9489d4856455086193e74c8b6d606b70ee8033244120675e420c1b23473e55d19699fe374b60b22ae624350a72fa76275')

prepare() {
	cd $pkgname
  	patch -Np1 < ../checkoverflow.patch
	sed -i 's/NOT BUILD_SHARED_LIBS/TRUE/' CMakeLists.txt
}

build() {
	cd $pkgname
	cmake -G Ninja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
 	ninja
}

package() {
  	cd $pkgname
  	DESTDIR="$pkgdir" ninja install
  	install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
