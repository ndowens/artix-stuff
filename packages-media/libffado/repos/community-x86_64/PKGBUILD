# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.2
pkgrel=3
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL3')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'glibmm' 'hicolor-icon-theme'
'libavc1394' 'libdbus-c++-1.so' 'libconfig++.so' 'libiec61883' 'libxml++'
'python-dbus')
makedepends=('jack' 'python-pyqt5' 'scons')
optdepends=('python-pyqt5: ffado-mixer')
provides=('libffado.so')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        "${pkgname}-2.4.2-python38.patch")
sha512sums=('96533ed15f95153bf34dffb55ab97cf0f3f6127c5b7a86edeeb55fe74540bbc23b40f494abfa41074176a04f18db311a56e436a0ef7b8afae612f2d1f641ab10'
            '5087aef1dd9bb2aa3c9c768d2ae8edffc11b6a3491f5df8df097b9ea0c7ff88d29f7c98d014d86805cd1b81fc5eb0f7b937a4607daf705a41310c20c66db93b3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' \
      -i support/xdg/ffado.org-ffadomixer.desktop
  # fix missing type conversion for python >= 3.8
  patch -Np1 -i "../${pkgname}-2.4.2-python38.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=True \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR='/usr/lib/python3.8/site-packages' \
        UDEVDIR='/usr/lib/udev/rules.d'
}

package() {
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
