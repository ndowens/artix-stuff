# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=fribidi
pkgver=1.0.9
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
url="https://github.com/fribidi/fribidi/"
arch=(x86_64)
license=(LGPL)
depends=(glibc)
makedepends=(git meson)
provides=(libfribidi.so)
_commit=f9e8e71a6fbf4a4619481284c9f484d10e559995  # tags/v1.0.9^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd fribidi
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}

prepare() {
  cd fribidi
}

build() {
  arch-meson fribidi build \
    -D docs=false
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
