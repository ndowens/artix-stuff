# Maintainer: Ivy Foster <code@escondida.tk>
# Reference: PKGBUILD(5)

pkgname=sndio
pkgver=1.6.0
pkgrel=1.2
pkgdesc='A small audio and MIDI framework part of the OpenBSD project'
arch=(x86_64)
url='http://www.sndio.org'
license=(ISC)

# sndio can be built without libbsd, but there are a bunch of individual
# ISC-licensed files by different authors to extract the licenses from
# if done that way. Licenses are complicated.
depends=(alsa-lib libbsd)

source=("http://www.sndio.org/sndio-$pkgver.tar.gz"
	"${pkgname}.sysusers.conf")
# checksums provided by packager
sha256sums=('99e0064ac11aceab24c73ed4630a31de401ff2f37689565b7b375682476f5bc1'
            '4dd07d579971c4a3b3091f109305e39b5e1a69e3bd62ead6229ef12e4384dba7')
backup=(etc/default/sndiod)

build() {
	cd "sndio-$pkgver"
	./configure --prefix=/usr --enable-alsa --with-libbsd
	make
}

package() {
	cd "sndio-$pkgver"

	make DESTDIR="$pkgdir/" install

	install -D -m 644 contrib/default.sndiod \
		"$pkgdir/etc/default/sndiod"

	install -d "$pkgdir/usr/share/licenses/sndio"
	# this is the most up-to-date license outside of bsd-compat,
	# which isn't being used in this build
	sed '/^ \*\//q' aucat/aucat.c > "$pkgdir/usr/share/licenses/sndio/LICENSE"

	install -Dm644 "${srcdir}/${pkgname}.sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
