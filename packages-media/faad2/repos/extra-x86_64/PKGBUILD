# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=faad2
pkgver=2.9.1
pkgrel=2
pkgdesc="ISO AAC audio decoder"
arch=('x86_64')
url="https://github.com/knik0/faad2"
license=('GPL2')
depends=('glibc')
provides=('faad' 'libfaad_drm.so' 'libfaad.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/knik0/faad2/archive/${pkgver//./_}.tar.gz)
sha256sums=('7fa33cff76abdda5a220ca5de0b2e05a77354f3b97f735193c2940224898aa9a')

prepare() {
  mv -v "${pkgname}-${pkgver//./_}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README*,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
