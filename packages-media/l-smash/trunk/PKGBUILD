# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=2.14.5
pkgrel=1.1
pkgdesc='MP4 muxer and other tools'
arch=('x86_64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
makedepends=('git')
provides=('liblsmash.so')
source=("git+https://github.com/l-smash/l-smash#tag=v${pkgver}"
        "out-of-bound.patch::https://github.com/l-smash/l-smash/commit/74329b46c401d500f3eff72675f4d55af393ad0d.patch"
        "improve-dts.patch::https://github.com/l-smash/l-smash/commit/f963b5a195e7561e732df0f72b7b0301277367eb.patch")
sha256sums=('SKIP'
            'f451f6bca98440a0b1097a2eb88aef6302957f557731efd0211554c1e7f97276'
            'e929634d747e72a172e5a4d4dafa8c172968148e1597dc37ca53ab5b37155645')

prepare() {
  cd "$pkgname"
  for _patch in "$srcdir"/*.patch ; do
    git apply --stat "$_patch"
  done
}

build() {
  cd "$pkgname"

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --disable-static \
    --extra-cflags="$CFLAGS" \
    --extra-ldflags="$LDFLAGS"
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/l-smash
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/l-smash/
}

# vim: ts=2 sw=2 et:
