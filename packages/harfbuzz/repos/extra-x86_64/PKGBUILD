# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=harfbuzz
pkgname=(harfbuzz harfbuzz-icu)
pkgver=2.6.5
pkgrel=1
pkgdesc="OpenType text shaping engine"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection gtk-doc
             ragel git python)
checkdepends=(python-fonttools python-setuptools)
_commit=f9bc373381ddf8553f943b774596ae5a53bf2641  # tags/2.6.5^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd harfbuzz
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd harfbuzz
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd harfbuzz
  ./configure \
    --prefix=/usr \
    --with-cairo \
    --with-freetype \
    --with-glib \
    --with-gobject \
    --with-graphite2 \
    --with-icu \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd harfbuzz
  TMPDIR="$srcdir" make check
  rm -rf "$srcdir"/tmp*
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite libglib-2.0.so libfreetype.so
           libgobject-2.0.so)
  provides=(libharfbuzz.so libharfbuzz-subset.so libharfbuzz-gobject.so)
  optdepends=('cairo: hb-view program')

  cd harfbuzz
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu() {
  pkgdesc="$pkgdesc (ICU integration)"
  depends=("harfbuzz=$pkgver-$pkgrel" icu libharfbuzz.so)
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}
