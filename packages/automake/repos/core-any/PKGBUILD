# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake
pkgver=1.16.2
pkgrel=1
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="https://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
makedepends=('autoconf')
checkdepends=('dejagnu' 'gcc-fortran' 'java-environment' 'vala' 'emacs' 'cscope'
              'python2' 'python-virtualenv')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        automake-1.15-dejagnu-testcase.patch)
sha512sums=('a4aa0e41ceaa7df5bc303a6004597fb158f4198594017cd2c586fd9f5a29233e081766bf22b7e4ef0d4c8c3d45a8591009427efa319b362922a958ac1ef6e27b'
            'SKIP'
            'fbea84532ebb9210b95d82104ee53d91bcf97e76c2ebad66794c22d8fd205b263dcba6c04aa87273cc40243ce4c5ae97df63417b52f37288167ad2b1aa80fd04')
validpgpkeys=('E1622F96D2BB4E58018EEF9860F906016E407573'   # Stefano Lattarini
              'F2A38D7EEB2B66405761070D0ADEE10094604D37'   # Mathieu Lirzin
              '155D3FC500C834486D1EEA677FD9FCCB000BEEEE')  # Jim Meyering

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# fix testsuite issue with resent dejagnu
	patch -p1 -i "$srcdir"/automake-1.15-dejagnu-testcase.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --build=$CHOST --prefix=/usr
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check PYTHON=/usr/bin/python2
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
