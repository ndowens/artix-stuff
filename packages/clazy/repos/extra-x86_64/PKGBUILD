# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.6
pkgrel=3
pkgdesc="Qt oriented code checker based on clang framework"
url="https://www.kdab.com/"
license=(GPL2)
depends=(clang)
makedepends=(cmake llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        clazy-llvm-10.patch::"https://cgit.kde.org/clazy.git/patch/?id=df41bd29"
        clazy-clang-cpp.patch::"https://cgit.kde.org/clazy.git/patch/CMakeLists.txt?id=0e295e5a")
sha256sums=('806d1befaddba82316f4ea34f1cba38bcc7545d0981007e3527c1ff1576dd0de'
            'SKIP'
            'b69cb69c508a2a02a996c6e4d426e6aacc5a6574c6b33315df4bfe6fed891a69'
            '7672f28c59bbe07920f36acb61efaa6ef19ed7f8fbf73e82d5740a023df6a1cf')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../clazy-llvm-10.patch # Fix build with LLVM 10
  patch -p1 -i ../clazy-clang-cpp.patch # link to libclang-cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCLANG_CLANG-CPP_LIB=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
