# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.2
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=('x86_64')
url="http://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("http://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('077006fa2da0d06d6383728112f2edef9684e9c8da56752e97cd45a11f838edd'
            'SKIP')
validpgpkeys=('90DFD6523C57373D81F63D19865688D038F02FC8') # Jean Delvare <khali@linux-fr.org>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
