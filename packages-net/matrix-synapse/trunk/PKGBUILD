# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=1.12.3
pkgrel=1

pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')

depends=('python-jsonschema' 'python-twisted' 'python-service-identity'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-daemonize' 'python-bcrypt' 'python-frozendict'
         'python-pillow' 'python-ujson'
         'python-pysaml2' 'python-setuptools'
         'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-idna' 'python-jinja'
         'python-bleach' 'python-typing_extensions')
checkdepends=('python-lxml' 'python-mock' 'python-parameterized')
optdepends=('perl: sync_room_to_group.pl'
            'python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-psutil: metrics'
            'python-pyjwt: jwt')

source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'sysusers-synapse.conf'
        'tmpfiles-synapse.conf')

md5sums=('88ad717394e695855e7aa6fa1ffc7f1c'
         'ecd9f66fb57fe1a2e1e2df07a460a35b'
         'e961c9ecad84a70345a57a7e9e6d5b09')

backup=('etc/synapse/log_config.yaml')
install=synapse.install

build() {
	cd synapse-$pkgver
	python setup.py build
}

check() {
	cd synapse-$pkgver
	PYTHONPATH=. trial3 tests
}

package() {

	cd synapse-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -dm755 -o 198 -g 198 "$pkgdir"/etc/synapse

	install -Dm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
	install -Dm644 "$srcdir"/tmpfiles-synapse.conf "$pkgdir"/usr/lib/tmpfiles.d/synapse.conf
}
