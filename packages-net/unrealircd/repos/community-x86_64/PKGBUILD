# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=5.0.3.1
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('x86_64')
url="https://www.unrealircd.org"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares' 'tre' 'pcre2' 'libnsl' 'argon2')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz
	unrealircd.tmpfiles.d
	unrealircd.sysusers.d
	install.pl)
sha256sums=('6bef40442585893dbb950c86ca6dac16324a3c29f4545d8169f42fa7c0715345'
            '75360541bc3aec9c1241c75e0ce60214c65cb618ffe5ca77144f01651ef5ab5b'
            '9e595176e63b301476982b1456d6ed065c479ff913b6743417ab8a9efdda0e3a'
            '43f07093ada8eb5c954f9e2e189610575906c690c0953fec041d4f2d31210d85')

prepare() {
  cd "$srcdir"/unrealircd-$pkgver
  sed -i \
    -e 's|$(INSTALL) -m 0700|$(INSTALL) -m 0755|g' \
    -e 's|$(INSTALL) -m 0600|$(INSTALL) -m 0644|g' \
    Makefile.in
}

build() {
export CFLAGS="${CFLAGS} -Wl,-z,now -Wl,-z,relro"
  cd "$srcdir"/unrealircd-$pkgver
  ./configure \
    --with-pidfile=/run/unrealircd/ircd.pid \
    --with-showlistmodes \
    --enable-ssl=/usr \
    --with-bindir=/usr/bin \
    --with-datadir=/var/lib/unrealircd \
    --with-confdir=/etc/unrealircd \
    --with-modulesdir=/usr/lib/unrealircd \
    --with-logdir=/var/log/unrealircd \
    --with-cachedir=/var/cache/unrealircd \
    --with-docdir=/usr/share/doc/unrealircd \
    --with-tmpdir=/tmp \
    --with-scriptdir=/usr \
    --with-nick-history=2000 \
    --with-sendq=3000000 \
    --with-permissions=0644 \
    --with-fd-setsize=1024 \
    --enable-dynamic-linking
  make
}

package() {
  cd "$srcdir"/unrealircd-$pkgver

  export pkgdir
  make INSTALL="$srcdir"/install.pl install
  mv "$pkgdir"/usr/unrealircd "$pkgdir"/etc/unrealircd/unrealircd
  cp "$pkgdir"/etc/unrealircd/examples/example.conf "$pkgdir"/etc/unrealircd/unrealircd.conf
  rm -rf "$pkgdir"/tmp

  install -Dm0644 "$srcdir"/unrealircd.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/unrealircd.conf
  install -Dm0644 "$srcdir"/unrealircd.sysusers.d "$pkgdir"/usr/lib/sysusers.d/unrealircd.conf
}
