# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.8
pkgrel=2
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'aspell' 'guile' 'lua' 'perl' 'python'
             'ruby' 'tcl')
optdepends=('aspell: spellchecker support'
            'guile: support for guile scripts'
            'lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for python scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "weechat-fix-sharedir.patch::https://github.com/weechat/weechat/pull/1461.patch")
sha512sums=('3bebb42118fca6b601f1de8f4d52378619a10e5c490f5b46f86d27a9680f00a56d48a1ade729e78d965cae589e6c80647c22414393000c7d3cfcc94a1e41c4df'
            'SKIP'
            'ad845cc5c0796b2dd32b7c5a0e7527da50ad0297111d9ddc2a711ea40b89f4ebe84c7f0e506e99a4c67e395fa0d14230de0d3f3b087e5c69739a0f564fa7a9ff')
b2sums=('756ce1719ecaa351a2780641758bb7587b45b8e6b16e5a82833009ddb27f71783a461cf89662e04118c75e2d372103eda5a11c98feb62d624b330b223bf22ede'
        'SKIP'
        'fbb085260af30694229bbc64d8859fd4d8bb4359d2e955bad775d182072b5e8c099d8581ccf20d5f8ec01bb7cd5bb3aee312cb21ceb713ef868d1830be29722c')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS}"

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://github.com/weechat/weechat/pull/1461
  # make /usr/share/weechat work, e.g. community/weechat-matrix
  patch -p1 -i ../weechat-fix-sharedir.patch
}

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PHP=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
