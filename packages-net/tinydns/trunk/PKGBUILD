# Maintainer: CupIvan <mail@cupivan.ru>

pkgname=tinydns
pkgver=0.3.1
pkgrel=1
pkgdesc='Tiny cache DNS server'
arch=('any')
url=https://github.com/CupIvan/${pkgname}
license=('MIT')
backup=(etc/${pkgname}.conf)
source=(https://github.com/CupIvan/${pkgname}/archive/v${pkgver}.tar.gz
        ${pkgname}.openrc)
sha256sums=('baf5d4246a22bd0c2d8ac3579cea18b50b3636762fecc452cad8614c13fc9807'
            'f7865099036c835b1e50796a80d37dd6b8c45cac5179fda28cdb56253488860a')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's:tinydns:& -Wl,-z,now,-z,relro:' Makefile	
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	
	install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
	install -Dm644 ${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
	install -Dm755 ${srcdir}/${pkgname}.openrc ${pkgdir}/etc/init.d/${pkgname}
	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
